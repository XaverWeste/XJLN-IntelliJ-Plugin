{
  parserClass="com.github.xjln.xjlnintellijplugin._XJLNParser"
  //parserUtilClass="com.github.xjln.xjlnintellijplugin.XJLNParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="XJLN"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.xjln.xjlnintellijplugin.psi"
  psiImplPackage="com.github.xjln.xjlnintellijplugin.psi.impl"

  elementTypeHolderClass="com.github.xjln.xjlnintellijplugin.psi.XJLNTypes"
  elementTypeClass="com.github.xjln.xjlnintellijplugin.psi.XJLNElementType"
  tokenTypeClass="com.github.xjln.xjlnintellijplugin.psi.XJLNTokenType"

  tokens = [
    //Keywords
    KEYWORD_USE = "use"
    KEYWORD_FROM = "from"
    KEYWORD_AS = "as"
    KEYWORD_DEF = "def"
    KEYWORD_PUBLIC = "public"
    KEYWORD_PROTECTED = "protected"
    KEYWORD_PRIVATE = "private"
    KEYWORD_STATIC = "static"
    KEYWORD_ABSTRACT = "abstract"
    KEYWORD_FINAL = "final"
    KEYWORD_SYNCHRONISED = "synchronised"
    KEYWORD_CLASS = "class"
    KEYWORD_DATA = "data"
    KEYWORD_TYPE = "type"
    KEYWORD_INTERFACE = "interface"
    KEYWORD_CONST = "const"
    KEYWORD_INIT = "init"
    KEYWORD_RETURN = "return"
    KEYWORD_END = "end"
    KEYWORD_IF = "if"
    KEYWORD_ELSE = "else"
    KEYWORD_WHILE = "while"
    KEYWORD_INT = "int"
    KEYWORD_DOUBLE = "double"
    KEYWORD_FLOAT = "float"
    KEYWORD_LONG = "long"
    KEYWORD_SHORT = "short"
    KEYWORD_BOOLEAN = "boolean"
    KEYWORD_CHAR = "char"
    KEYWORD_BYTE = "byte"
    KEYWORD_EXTENDS = "extends"
    KEYWORD_CONTAINS = "contains"
    KEYWORD_IMPLEMENTS = "implements"
    KEYWORD_NULL = "null"
    KEYWORD_THIS = "this"
    //TODO true false

    LAMBDA = "->"
    NEW_LINE = "regexp:\n"
    IDENTIFIER = "regexp:[a-zA-Z][a-zA-Z0-9]*"
    NUMBER = "regexp:[0-9]+(\.[0-9])*[idslf]?"
    OPERATOR = "regexp:[-+*/%&|<>=!^]+"
    CHAR = "regexp:'.'"
    COMMENT = "regexp:#[^[#\n]]*#?"
    STRING = "regexp:\"[^\"]*\""
    COMMA = ","
  ]
}

xjlnFile ::= argument? (NEW_LINE argument?)*

argument ::= use | field | data | type | interface | clazz | method | COMMENT

use ::= KEYWORD_USE (single_use | multi_use)
single_use ::= ((class_name KEYWORD_FROM path) | path) (KEYWORD_AS alias)?
class_name ::= IDENTIFIER
alias ::= IDENTIFIER
multi_use ::= "{" class_name (COMMA class_name)* "}" KEYWORD_FROM path
path ::= IDENTIFIER ("/" IDENTIFIER)*

data ::= data_type_def KEYWORD_CONST? KEYWORD_DATA clazz_name generics? "=" "[" parameterlist? "]"
type ::= data_type_def KEYWORD_TYPE clazz_name "=" type_value ("|" type_value)*
interface ::= KEYWORD_DEF access_modifier? KEYWORD_ABSTRACT? KEYWORD_INTERFACE clazz_name generics? "{" NEW_LINE ((abstract_method | COMMENT)? NEW_LINE)* "}"
clazz ::= KEYWORD_DEF access_modifier? KEYWORD_CLASS (KEYWORD_ABSTRACT | KEYWORD_FINAL)* clazz_name generics? extends? contains? implements? "{" NEW_LINE ((field | abstract_method | method | init | COMMENT)? NEW_LINE)* "}"

clazz_name ::= IDENTIFIER
type_value ::= IDENTIFIER

data_type_def ::= KEYWORD_DEF access_modifier? KEYWORD_FINAL?

extends ::= KEYWORD_EXTENDS class_name
contains ::= KEYWORD_CONTAINS class_name | ("{" (class_name (COMMA class_name)*)? "}")
implements ::= KEYWORD_IMPLEMENTS class_name | ("{" (class_name (COMMA class_name)*)? "}")

generics ::= "<" generic (COMMA generic)* ">"
generic ::= class_name alias

field ::= access_modifier? KEYWORD_STATIC? KEYWORD_CONST? datatype ("<" class_name (COMMA class_name)* ">")? IDENTIFIER ("=" value)?
abstract_method ::= KEYWORD_DEF access_modifier? KEYWORD_ABSTRACT IDENTIFIER generics? "(" parameterlist? ")" ("::" datatype)?
method ::= KEYWORD_DEF access_modifier? (KEYWORD_STATIC | KEYWORD_SYNCHRONISED)* (IDENTIFIER | OPERATOR) generics? "(" parameterlist? ")" ("::" datatype)? (method_mult | method_short | method_long)
init ::= KEYWORD_INIT "[" parameterlist? "]" (method_short | method_long)

method_short ::= ("=" | LAMBDA) statement
method_long ::= NEW_LINE (statement NEW_LINE+)* KEYWORD_END
method_mult ::= "{" NEW_LINE method_mult_arg+ "}"
method_mult_arg ::= "(" ((OPERATOR? value) (COMMA (OPERATOR? value))*)? ")" method_short | method_long NEW_LINE

access_modifier ::= KEYWORD_PUBLIC | KEYWORD_PROTECTED | KEYWORD_PRIVATE

parameterlist ::= parameter (COMMA parameter)*
parameter ::= datatype IDENTIFIER

datatype ::= (IDENTIFIER ("<" class_name (COMMA class_name)* ">")?) | KEYWORD_INT | KEYWORD_SHORT | KEYWORD_LONG | KEYWORD_FLOAT | KEYWORD_DOUBLE | KEYWORD_BOOLEAN | KEYWORD_CHAR | KEYWORD_BYTE | arrayType
arrayType ::= "[" datatype "]"
value ::= CHAR | NUMBER | STRING | KEYWORD_NULL | call

statement ::= (KEYWORD_RETURN calc) | var | calc | if | while
calc ::= calcArg (OPERATOR calcArg)*
calcArg ::= (("(" calc ")") | value | call | arr)
call ::= (KEYWORD_THIS | (IDENTIFIER ("<" class_name (COMMA class_name)* ">")? (("[" (calc (COMMA calc)*)? "]") | ("(" (calc (COMMA calc)*)? ")"))?)) (":" callArg)*
callArg ::= ((IDENTIFIER ("(" (calc (COMMA calc)*)? ")")?) | NUMBER | ("(" calc ")"))
var ::= KEYWORD_CONST? datatype? IDENTIFIER ("=" (arr | calc))?
arr ::= ("{" (value (COMMA value)*)? "}") | ("[" datatype (COMMA (NUMBER | calc))* "]")

if ::= KEYWORD_IF calc ((LAMBDA statement (NEW_LINE else)?) | (statement_block (else | KEYWORD_END)))
else ::= KEYWORD_ELSE (KEYWORD_IF calc)? ((LAMBDA statement (NEW_LINE else)?) | (statement_block (else | KEYWORD_END)))

while ::= KEYWORD_WHILE calc ((statement_block KEYWORD_END) | (LAMBDA statement))

statement_block ::= NEW_LINE (statement? NEW_LINE+)*